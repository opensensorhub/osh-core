description = 'OSH Admin Vaadin WidgetSet'

configurations {
  vaadin {
    extendsFrom compileOnly
  }
}

tasks.osgi.enabled = false

dependencies {
  compileOnly 'com.vaadin:vaadin-shared:' + vaadinVersion
  compileOnly 'com.vaadin:vaadin-server:' + vaadinVersion
  compileOnly 'com.vaadin:vaadin-client-compiled:' + vaadinVersion
  compileOnly 'com.vaadin:vaadin-compatibility-client:' + vaadinVersion
  compileOnly 'com.vaadin:vaadin-compatibility-client-compiled:' + vaadinVersion
  compileOnly 'com.vaadin:vaadin-themes:' + vaadinVersion
  vaadin 'com.vaadin:vaadin-client-compiler:' + vaadinVersion 
}

task compileWidgetset(type: JavaExec) {
  def inputSource = sourceSets.main.resources.srcDirs
  def outputFolder = "$projectDir/src/main/resources/VAADIN/widgetsets"
  inputs.files(inputSource)
  outputs.dir(outputFolder)
  
  mainClass = 'com.vaadin.tools.WidgetsetCompiler'
  classpath inputSource
  classpath configurations.vaadin
  jvmArgs '-Xmx512M', '-Xss512M', '-Dgwt.persistentunitcachedir='+project.buildDir.canonicalPath
  args '-war', outputFolder
  args '-deploy', "$projectDir/build/deploy"
  args '-optimize', '0'
  args '-localWorkers', '' + Runtime.getRuntime().availableProcessors()
  args '-logLevel', 'WARN'
  args 'org.sensorhub.ui.SensorHubWidgetSet'
}

task compileTheme(type: JavaExec) {
  def inputScss = "$projectDir/src/main/resources/VAADIN/themes/sensorhub/styles.scss"
  def outputCss = "$projectDir/src/main/resources/VAADIN/themes/sensorhub/styles.css"
  inputs.file(inputScss)
  outputs.file(outputCss)
  
  mainClass = 'com.vaadin.sass.SassCompiler'
  classpath sourceSets.main.resources.srcDirs
  classpath configurations.vaadin
  jvmArgs '-Xmx64M', '-Xss64M'
  args inputScss
  args outputCss
  /*doFirst {
    println commandLine
  }*/
}

task cleanVaadin(type: Delete) {
  delete fileTree("$projectDir/src/main/resources/VAADIN/themes") {
      include '**/*.css'
  }
  delete "$projectDir/src/main/resources/VAADIN/widgetsets"
}

compileWidgetset.dependsOn compileTheme
processResources.dependsOn compileWidgetset
processResources.dependsOn compileTheme
clean.dependsOn cleanVaadin